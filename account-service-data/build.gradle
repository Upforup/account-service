import groovy.xml.MarkupBuilder
import org.jooq.codegen.GenerationTool

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'org.jooq:jooq-codegen:3.11.10'
        classpath 'org.jooq:jooq-meta:3.11.10'
        classpath 'com.h2database:h2:1.4.199'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'org.liquibase.gradle' version '2.0.1'
    id 'jacoco'

}

def codeGenUrl = 'jdbc:h2:~/codegen'



task jooqGenerate(dependsOn: update) {

    def writer = new StringWriter()
    new MarkupBuilder(writer)
            .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.11.0.xsd') {
        jdbc() {
            driver('org.h2.Driver')
            url(codeGenUrl)
            user('sa')
            password('')
        }

        generator() {
            database() {
                name('org.jooq.meta.h2.H2Database')
                includes('ACCOUNT.* | OPERATION.* | ACCOUNT_MOVEMENT.*')
                excludes('')
                inputSchema('PUBLIC')
            }

            target() {
                packageName('my.backend.test.solution.data.generated')
                directory("$project.projectDir/src/main/java/")
            }
            strategy() {
                matchers() {
                    tables() {
                        table() {
                            tableClass() {
                                transform('PASCAL')
                                expression('$0_TABLE')
                            }
                        }
                    }
                }
            }
        }
    }

    doLast {
        GenerationTool.generate(writer.toString())
    }
}



liquibase {
    activities {
        main {
            changeLogFile "db.changelog-master.yaml"
            url codeGenUrl
            driver 'org.h2.Driver'
            username 'sa'
            password ''
            logLevel "debug"
            classpath "${project.projectDir}/db/changelog"
            contexts "liquibase"
        }

    }

}

task copyChangelog(type: Copy) {
    from "${project.projectDir}/db/"
    into "${project.buildDir}/resources/main"
}




dependencies {

    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.3'
    liquibaseRuntime 'com.h2database:h2:1.4.199'

    compileOnly('org.projectlombok:lombok:1.18.6')

    implementation('org.slf4j:slf4j-api:1.7.25')
    implementation(project(':account-service-core'))
    compile('org.liquibase:liquibase-core:3.6.3')
    compile('org.jooq:jooq:3.11.10')
    compile('com.zaxxer:HikariCP:3.3.1')
    compile('com.h2database:h2:1.4.199')
    testCompile('junit:junit:4.12')

}


tasks.processResources.dependsOn tasks.copyChangelog
